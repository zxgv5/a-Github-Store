import org.jetbrains.compose.desktop.application.dsl.TargetFormat
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import java.util.Properties

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.composeHotReload)
    alias(libs.plugins.kotlinSerialization)
    alias(libs.plugins.kotest)
    alias(libs.plugins.ksp)
    alias(libs.plugins.androidx.room)
}

val appVersionName = "1.5.1"
val appVersionCode = 10

val localProps = Properties().apply {
    val file = rootProject.file("local.properties")
    if (file.exists()) file.inputStream().use { this.load(it) }
}
val localGithubClientId =
    (localProps.getProperty("GITHUB_CLIENT_ID") ?: "Ov23linTY28VFpFjFiI9").trim()

// Generate BuildConfig for JVM (Configuration Cache Compatible)
val generateJvmBuildConfig = tasks.register("generateJvmBuildConfig") {
    val outputDir = layout.buildDirectory.dir("generated/buildconfig/jvm")
    val clientId = localGithubClientId
    val versionName = appVersionName

    outputs.dir(outputDir)

    doLast {
        val file = outputDir.get().asFile.resolve("zed/rainxch/githubstore/BuildConfig.kt")
        file.parentFile.mkdirs()
        file.writeText(
            """
            package zed.rainxch.githubstore
            
            object BuildConfig {
                const val GITHUB_CLIENT_ID = "$clientId"
                const val VERSION_NAME = "$versionName"
            }
        """.trimIndent()
        )
    }
}

kotlin {
    androidTarget {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_21)
        }
    }

    jvm()

    sourceSets {
        androidMain.dependencies {
            implementation(compose.preview)
            implementation(libs.androidx.activity.compose)
            // Koin DI
            implementation(libs.koin.android)
            implementation(libs.koin.androidx.compose)
            // Ktor client for Android
            implementation(libs.ktor.client.core)
            implementation(libs.ktor.client.okhttp)
            implementation(libs.ktor.client.content.negotiation)
            implementation(libs.ktor.serialization.kotlinx.json)
            implementation(libs.kotlinx.serialization.json)
            // Secure storage (direct coordinate to avoid catalog accessor mismatch)
            implementation(libs.androidx.security.crypto)
            implementation(libs.core.splashscreen)
        }
        commonMain.dependencies {
            implementation(compose.runtime)
            implementation(compose.foundation)
            implementation(libs.material3)
            implementation(compose.materialIconsExtended)
            implementation(compose.ui)
            implementation(compose.components.resources)
            implementation(compose.components.uiToolingPreview)
            implementation(libs.androidx.lifecycle.viewmodelCompose)
            implementation(libs.androidx.lifecycle.runtimeCompose)

            // Koin DI
            implementation(libs.koin.core)
            implementation(libs.koin.compose)
            implementation(libs.koin.compose.viewmodel)

            // HTTP and serialization
            implementation(libs.ktor.client.core)
            implementation(libs.ktor.client.content.negotiation)
            implementation(libs.ktor.serialization.kotlinx.json)
            implementation(libs.kotlinx.serialization.json)
            implementation(libs.kotlinx.coroutines.core)

            // Logging
            implementation(libs.kermit)

            // Image Loading
            implementation(libs.landscapist.core)
            implementation(libs.landscapist.image)

            // Date-time
            implementation(libs.kotlinx.datetime)

            // Navigation 3
            implementation(libs.navigation.compose)
            implementation(libs.jetbrains.navigation3.ui)
            implementation(libs.jetbrains.lifecycle.viewmodel.compose)
            implementation(libs.jetbrains.lifecycle.viewmodel)
            implementation(libs.jetbrains.lifecycle.viewmodel.navigation3)

            // Markdown
            implementation(libs.multiplatform.markdown.renderer)
            implementation(libs.multiplatform.markdown.renderer.coil3)

            // Liquid
            implementation(libs.liquid)

            // Data store
            implementation(libs.androidx.datastore)
            implementation(libs.androidx.datastore.preferences)

            // Room
            implementation(libs.androidx.room.runtime)
            implementation(libs.sqlite.bundled)

            implementation(libs.kotlinx.coroutinesSwing)
        }
        commonTest.dependencies {
            implementation(libs.kotlin.test)
            implementation(libs.kotest.framework.engine)
            implementation(libs.kotest.assertions.core)
        }
        jvmTest.dependencies {
            implementation(libs.kotest.runner.junit5)
        }
        jvmMain {
            kotlin.srcDir(layout.buildDirectory.dir("generated/buildconfig/jvm"))

            dependencies {
                implementation(compose.desktop.currentOs)
                // Koin core
                implementation(libs.koin.core)
                // Ktor client for JVM Desktop
                implementation(libs.ktor.client.core)
                implementation(libs.ktor.client.java)
                implementation(libs.ktor.client.content.negotiation)
                implementation(libs.ktor.serialization.kotlinx.json)
                implementation(libs.kotlinx.serialization.json)
            }
        }
    }
}

afterEvaluate {
    tasks.matching { it.name.contains("kspKotlinJvm") || it.name == "compileKotlinJvm" }.configureEach {
        dependsOn(generateJvmBuildConfig)
    }
}

tasks.named("compileKotlinJvm") {
    dependsOn(generateJvmBuildConfig)
}

android {
    namespace = "zed.rainxch.githubstore"
    compileSdk = libs.versions.android.compileSdk.get().toInt()

    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }
    defaultConfig {
        applicationId = "zed.rainxch.githubstore"
        minSdk = libs.versions.android.minSdk.get().toInt()
        targetSdk = libs.versions.android.targetSdk.get().toInt()
        versionCode = appVersionCode
        versionName = appVersionName

        buildConfigField("String", "GITHUB_CLIENT_ID", "\"${localGithubClientId}\"")
        buildConfigField("String", "VERSION_NAME", "\"${appVersionName}\"")
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
    buildTypes {
        getByName("release") {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    buildFeatures {
        buildConfig = true
    }
}

tasks.named<Test>("jvmTest") {
    useJUnitPlatform()
}

dependencies {
    debugImplementation(compose.uiTooling)

    ksp(libs.androidx.room.compiler)
}

room {
    schemaDirectory("$projectDir/schemas")
}

compose.desktop {
    application {
        mainClass = "zed.rainxch.githubstore.MainKt"
        nativeDistributions {
            packageName = "GitHub-Store"
            packageVersion = appVersionName
            vendor = "rainxchzed"
            includeAllModules = true
            targetFormats(
                TargetFormat.Dmg,
                TargetFormat.Pkg,
                TargetFormat.Exe,
                TargetFormat.Msi,
                TargetFormat.Deb,
                TargetFormat.Rpm,
                TargetFormat.AppImage
            )
            windows {
                iconFile.set(project.file("logo/app_icon.ico"))
                menuGroup = "Github Store"
                shortcut = true
                perUserInstall = true
            }
            macOS {
                iconFile.set(project.file("logo/app_icon.icns"))
                bundleID = "zed.rainxch.githubstore"
            }
            linux {
                iconFile.set(project.file("logo/app_icon.png"))
                appRelease = appVersionCode.toString()
                debPackageVersion = appVersionName
                menuGroup = "Development"
                appCategory = "Development"
            }
        }
    }
}

val appId = "zed.rainxch.githubstore"
val appName = "GitHub-Store"
val flatpakDir = layout.buildDirectory.dir("flatpak")
val flatpakSourceDir = layout.buildDirectory.dir("flatpak-source")

// Define copy tasks separately to avoid configuration cache issues
val copyFlatpakBinary = tasks.register<Copy>("copyFlatpakBinary") {
    dependsOn("packageAppImage")

    val appImageOutput = layout.buildDirectory.dir("compose/binaries/main/app/$appName")

    from(appImageOutput)
    into(flatpakDir.map { it.asFile.resolve("build") })

    doFirst {
        val targetDir = outputs.files.singleFile
        if (targetDir.exists()) {
            targetDir.deleteRecursively()
        }
        targetDir.mkdirs()
    }
}

val resizeFlatpakIcon = tasks.register<Exec>("resizeFlatpakIcon") {
    dependsOn(copyFlatpakBinary)

    val resourcesDir = layout.projectDirectory.dir("src/jvmMain/resources/flatpak")
    val outputDir = flatpakDir.map { it.asFile.resolve("build") }

    doFirst {
        val inputIcon = resourcesDir.file("app_icon.png").asFile
        val outputIcon = outputDir.get().resolve("app_icon.png")
        outputIcon.parentFile.mkdirs()

        // Copy and resize icon
        inputIcon.copyTo(outputIcon, overwrite = true)
    }

    workingDir(flatpakDir.map { it.asFile.resolve("build") })
    commandLine("convert", "app_icon.png", "-resize", "512x512", "app_icon_resized.png")

    doLast {
        val buildDir = flatpakDir.get().asFile.resolve("build")
        val resized = buildDir.resolve("app_icon_resized.png")
        val target = buildDir.resolve("app_icon.png")
        if (resized.exists()) {
            resized.copyTo(target, overwrite = true)
            resized.delete()
        }
    }

    // Skip if ImageMagick not available
    onlyIf {
        try {
            val result = ProcessBuilder("which", "convert").start().waitFor()
            result == 0
        } catch (e: Exception) {
            false
        }
    }
}

val copyFlatpakResources = tasks.register<Copy>("copyFlatpakResources") {
    dependsOn(resizeFlatpakIcon)

    val resourcesDir = layout.projectDirectory.dir("src/jvmMain/resources/flatpak")

    from(resourcesDir) {
        include("manifest.yml", "*.desktop", "*.xml")
    }
    into(flatpakDir.map { it.asFile.resolve("build") })

    rename("manifest.yml", "$appId.yml")
}

tasks.register<Exec>("packageFlatpak") {
    dependsOn(copyFlatpakResources)
    group = "build"
    description = "Build Flatpak for Linux desktop (binary-based)"

    val buildDir = flatpakDir.map { it.asFile.resolve("build") }
    val repoDir = flatpakDir.map { it.asFile.resolve("repo") }

    workingDir(buildDir)
    commandLine(
        "flatpak-builder",
        "--install-deps-from=flathub",
        "--repo=${repoDir.get().absolutePath}",
        "--force-clean",
        "--disable-rofiles-fuse",
        "build/${appId}",
        "$appId.yml"
    )
}

tasks.register<Exec>("exportFlatpak") {
    dependsOn("packageFlatpak")
    group = "build"
    description = "Export Flatpak bundle (binary-based)"

    val versionName = appVersionName

    workingDir(flatpakDir)
    commandLine("flatpak", "build-bundle", "repo", "github-store-${versionName}.flatpak", appId, "master")
}

tasks.register<Exec>("runFlatpak") {
    dependsOn("packageFlatpak")
    group = "run"
    description = "Run the Flatpak locally (binary-based)"

    val buildDir = flatpakDir.map { it.asFile.resolve("build") }

    workingDir(buildDir)
    commandLine("flatpak-builder", "--run", "build/${appId}", "${appId}.yml", "GitHub-Store")
}

// Source-based tasks for Flathub submission
val copyFlatpakSourceResources = tasks.register<Copy>("copyFlatpakSourceResources") {
    val resourcesDir = layout.projectDirectory.dir("src/jvmMain/resources/flatpak")

    from(resourcesDir) {
        include("zed.rainxch.githubstore.yml", "*.desktop", "*.xml", "app_icon.png")
    }
    into(flatpakSourceDir.map { it.asFile.resolve("build") })

    doFirst {
        val targetDir = outputs.files.singleFile
        if (targetDir.exists()) {
            targetDir.deleteRecursively()
        }
        targetDir.mkdirs()
    }
}

val copyGeneratedSources = tasks.register<Copy>("copyGeneratedSources") {
    dependsOn(copyFlatpakSourceResources)

    val sourcesFile = layout.projectDirectory.file("generated-sources.json")

    from(sourcesFile)
    into(flatpakSourceDir.map { it.asFile.resolve("build") })

    onlyIf {
        sourcesFile.asFile.exists()
    }
}

tasks.register<Exec>("packageFlatpakSource") {
    dependsOn(copyGeneratedSources)
    group = "build"
    description = "Build Flatpak for Linux desktop (source-based for submission)"

    val buildDir = flatpakSourceDir.map { it.asFile.resolve("build") }
    val repoDir = flatpakSourceDir.map { it.asFile.resolve("repo") }

    workingDir(buildDir)
    commandLine(
        "flatpak-builder",
        "--install-deps-from=flathub",
        "--repo=${repoDir.get().absolutePath}",
        "--force-clean",
        "build/${appId}",
        "$appId.yml"
    )
}

tasks.register<Exec>("exportFlatpakSource") {
    dependsOn("packageFlatpakSource")
    group = "build"
    description = "Export Flatpak bundle (source-based)"

    val versionName = appVersionName

    workingDir(flatpakSourceDir)
    commandLine("flatpak", "build-bundle", "repo", "github-store-${versionName}-source.flatpak", appId, "master")
}

tasks.register<Exec>("runFlatpakSource") {
    dependsOn("packageFlatpakSource")
    group = "run"
    description = "Run the Flatpak locally (source-based)"

    val buildDir = flatpakSourceDir.map { it.asFile.resolve("build") }

    workingDir(buildDir)
    commandLine("flatpak-builder", "--run", "build/${appId}", "${appId}.yml", "GitHub-Store")
}
